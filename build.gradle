plugins {
    id 'io.spring.dependency-management' version '1.1.6'
}

ext {
    springCloudVersion = '2025.0.0'
    lombokVersion      = '1.18.34'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

configure(subprojects.findAll { it.path != ':services' }) {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain { languageVersion = JavaLanguageVersion.of(17) }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    }

    pluginManager.withPlugin('org.springframework.boot') {
        dependencies {
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }
}
